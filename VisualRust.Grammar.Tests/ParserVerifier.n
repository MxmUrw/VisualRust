using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using System.IO;

using System.Threading;

using Nitra.Declarations;
using Nitra.ProjectSystem;

using Microsoft.VisualStudio.TestTools.UnitTesting;

namespace VisualRust.Grammar.Tests
{
    module ParserVerifier
    {
        public VerifyPositiveFile(filename : string) : void
        {
            def file = ParseFileWithExtendedStack(filename);            
            Assert.IsTrue(file.ParseResult.IsSuccess, GetMessage(file));
        }        
        
        public VerifyNegativeFile(filename : string) : void
        {
            def file = ParseFileWithExtendedStack(filename);            
            Assert.IsFalse(file.ParseResult.IsSuccess, GetMessage(file))
        }        

        private ParseFileWithExtendedStack(filename : string) : FsFile[IAst]
        {
            mutable file      = None();
            mutable exception = None();
            
            def maxStackSize = 32 * 1024 * 1024;
            def thread = Thread(() =>
                try
                {
                    file = Some(FsFile(filename, NitraRust.Instance))
                }
                catch
                {
                    e => exception = Some(e)
                },
                maxStackSize);
                
            thread.Start();
            thread.Join();
            
            exception.Iter(x => throw x);
            
            file.Value
        }
        
        private GetMessage(file : FsFile[IAst]) : string
        {
            Uri(Path.GetFullPath(file.FilePath)).AbsoluteUri + ":\n" + $<#..$(file.GetCompilerMessages();"\n")#>
        }
    }
}
